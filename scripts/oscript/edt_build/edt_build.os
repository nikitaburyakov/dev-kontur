#Использовать ibcmdrunner
#Использовать gitrunner
#Использовать logos
#Использовать messenger

Перем ВерсияПлатформы;          // Версия платформы проекта
Перем ПутьКEDT;                 // Путь до 1cedtcli
Перем КаталогСборки;            // Путь до временного каталога
Перем ВременныйКаталог;         // Путь до временного каталога
Перем СведенияОКонфигурации;    // Сведения о конфигурации
Перем СведенияОРасширениях;     // Сведения о расширениях
Перем КаталогПроекта;           // Каталог проекта
Перем КаталогРелизов;           // Каталог релизов
Перем КаталогФайловВыгрузки;    // Каталог файлов выгрузки
Перем Лог;                      // Логирование
Перем ВрИмяФайлаЛога;           // Имя временного файла лога
Перем TelegramBotID;            // ИД бота Telegram для уведомлений
Перем TelegramChannelID;        // ИД канала Telegram для уведомлений
Перем ИмяПроекта;               // Имя проекта GitLab
Перем ИмяВетки;                 // Имя ветки сборки
Перем PipelineID;               // Идентификатор пайплайна 

Процедура ВыполнитьВыгрузкуПроектаВФайлы()
    
    ВремяНачала = Формат(ТекущаяДата(), "ДТ");
    Лог.Информация(СтрШаблон("Начало выгрузки файлов проекта EDT в файлы конфигураций: %1", ВремяНачала));

    КаталогРабочейОбласти = ОбъединитьПути(ВременныйКаталог, "workspace");
    КаталогФайловВыгрузки = ОбъединитьПути(ВременныйКаталог, "unload_xml");
        
    КомандыВыполнения = Новый Массив;
    
    ШаблонИсполненияКоманды = "%1 -data %2 -timeout 5400 -vmargs -Xmx8g -DisableProjectChecks=true -DisableFTS=true -command export --project %3 --configuration-files %4";
    
    // Основная конфигурация
    СведенияОКонфигурации.Вставить("КаталогВыгрузкиXML", 
        ОбъединитьПути(КаталогФайловВыгрузки, СведенияОКонфигурации.ИмяФормированияПути));
    СтрокаКоманды = СтрШаблон(ШаблонИсполненияКоманды,
                ПутьКEDT,                                        // -- путь к исполняемому 1cedtcli
                КаталогРабочейОбласти,                           // -- путь до рабочей области    
                СведенияОКонфигурации.ПутьВнутриПроекта,         // -- путь до проекта основной конфигурации
                СведенияОКонфигурации.КаталогВыгрузкиXML);
    КомандыВыполнения.Добавить(СтрокаКоманды);                   // -- путь до каталога файлов выгрузки

    // Расширения
    Для Каждого ОписаниеРасширения Из СведенияОРасширениях Цикл
        ОписаниеРасширения.Вставить("КаталогВыгрузкиXML",
            ОбъединитьПути(КаталогФайловВыгрузки, ОписаниеРасширения.ИмяФормированияПути));
        СтрокаКоманды = СтрШаблон(ШаблонИсполненияКоманды,
                ПутьКEDT,                                        // -- путь к исполняемому 1cedtcli
                КаталогРабочейОбласти,                           // -- путь до рабочей области    
                ОписаниеРасширения.ПутьВнутриПроекта,            // -- путь до проекта расширения конфигурации
                ОписаниеРасширения.КаталогВыгрузкиXML);
        КомандыВыполнения.Добавить(СтрокаКоманды);               // -- путь до каталога файлов выгрузки    
    КонецЦикла;

    Для Каждого КомандаВыполнения Из КомандыВыполнения Цикл
        Команда = Новый Команда();
        Команда.ПоказыватьВыводНемедленно(Ложь);
        Команда.УстановитьСтрокуЗапуска(КомандаВыполнения);
        Команда.УстановитьИсполнениеЧерезКомандыСистемы(Истина);
        КодВозврата = Команда.Исполнить();
        Если НЕ КодВозврата = 0 Тогда
            ВыводКоманды = Команда.ПолучитьВывод();
            Лог.Ошибка("НЕ УДАЛОСЬ ВЫПОЛНИТЬ ЭКСПОРТ EDT: " + ВыводКоманды);
            ВызватьИсключение ВыводКоманды;
        КонецЕсли;
    КонецЦикла;

    ВремяОкончания = Формат(ТекущаяДата(), "ДТ");
    Лог.Информация(СтрШаблон("Окончание выгрузки файлов проекта EDT в файлы конфигураций: %1", ВремяОкончания));

КонецПроцедуры

Процедура ВыполнитьСборкуИзИсходников()
    
    ВремяНачала = Формат(ТекущаяДата(), "ДТ");
    Лог.Информация(СтрШаблон("Начало сборки файлов конфигурации из исходников: %1", ВремяНачала));

    КаталогИБ = ОбъединитьПути(ВременныйКаталог, "db_temp");
    СоздатьКаталог(КаталогИБ);

    Лог.Информация("Создан каталог временной ИБ: " + КаталогИБ);
    
    УправлениеИБ = Новый УправлениеИБ(ВерсияПлатформы);
    УправлениеИБ.УстановитьПараметрыФайловойИБ(КаталогИБ);
    Если НайтиФайлы(КаталогИБ, "*.1CD").Количество() = 0 Тогда
        Лог.Информация("ИБ не найдена"); 
        Лог.Информация("Создается ИБ с параметром загрузки файлов основной конфигурации по пути: " + КаталогИБ);
        УправлениеИБ.СоздатьИБИзФайловКонфигурации(СведенияОКонфигурации.КаталогВыгрузкиXML);
    Иначе
        Лог.Информация("Выполняется загрузка файлов в ИБ: " + КаталогИБ);
        УправлениеИБ.ЗагрузитьКонфигурациюИзФайлов(СведенияОКонфигурации.КаталогВыгрузкиXML);
    КонецЕсли;

    СоздатьКаталог(КаталогРелизов);

    ИмяФайлаВыгрузкиОсновнойКонфигурации = ОбъединитьПути(КаталогРелизов, "1cv8.cf");
    Лог.Информация("Начало выгрузки .cf в файл: " + ИмяФайлаВыгрузкиОсновнойКонфигурации);
    УправлениеИБ.ВыгрузитьКонфигурациюВФайл(ИмяФайлаВыгрузкиОсновнойКонфигурации);

    Для Каждого ОписаниеРасширения Из СведенияОРасширениях Цикл
        ИмяРасширения = ОписаниеРасширения.Имя;
        ИмяФайлаРелиза = ОбъединитьПути(КаталогРелизов, СтрШаблон("%1.cfe", ОписаниеРасширения.Имя));
        Лог.Информация(СтрШаблон("Начало загрузки расширения %1 из файлов", ИмяРасширения));    
        УправлениеИБ.ЗагрузитьКонфигурациюИзФайлов(ОписаниеРасширения.КаталогВыгрузкиXML,
            ИмяРасширения);
        Лог.Информация(СтрШаблон("Начало выгрузки расширения %1 в .cfe: %2", ИмяРасширения, ИмяФайлаРелиза));    
        УправлениеИБ.ВыгрузитьКонфигурациюВФайл(ИмяФайлаРелиза, 
            ИмяРасширения);   
    КонецЦикла;

    ВремяОкончания = Формат(ТекущаяДата(), "ДТ");
    Лог.Информация(СтрШаблон("Окончание сборки файлов конфигурации из исходников: %1", ВремяОкончания));

КонецПроцедуры

// Проверяет установленные переменные среды (для целей отладки)
// Если переменная не установлена, выполняем установкой по текущей локальной конфигурации
Процедура ПроверитьУстановитьПеременныеСреды()

    Лог.Информация("Устанавливаются переменные среды");
    
    Переменные = Новый Соответствие;
        
    // Задаем переменные для тестирования
    // В проде переменные среды должны быть заданы в настройках CI проекта
    // Предопределенные переменные gitlab-ci:
    // * CI_PROJECT_DIR - каталог проекта
    // * CI_PIPELINE_ID - идентификатор pipeline
    // * CI_JOB_ID      - идентификатор job
    // * CI_BUILDS_DIR  - каталог сборки - не задаем для тестирования, инициализируем в каталоге сценария
    Переменные.Вставить("CI_PROJECT_DIR",   "E:\Buryakov\git\testedt"); 
    Переменные.Вставить("ONEC_VERSION",     "8.3.27.1859"); // -- версия платформы
    Переменные.Вставить("ONEC_EDTCLI_PATH", 
        """C:\Program Files\1C\1CE\components\1c-edt-2025.1.4+15-x86_64\1cedtcli""");
    Переменные.Вставить("CI_PIPELINE_ID",   "TEST_PIPELINE_ID");
    Переменные.Вставить("CI_JOB_ID",        "TEST_JOB_ID");   
    
    Для Каждого КлючИЗначение Из Переменные Цикл
        ЗначениеПеременной = ПолучитьПеременнуюСреды(КлючИЗначение.Ключ);
        Если ЗначениеПеременной = Неопределено Тогда
            Лог.Информация(СтрШаблон("Переменная среды %1 не была установлена", КлючИЗначение.Ключ));
            УстановитьПеременнуюСреды(КлючИЗначение.Ключ, КлючИЗначение.Значение);
        КонецЕсли;
    КонецЦикла;

    // ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ КАТАЛОГОВ
    // Структура проекта
    // <Каталог программы>\            # Раннер или путь скрипта   
    // ├── builds\                     # CI_BUILDS_DIR - ОСНОВНОЙ КАТАЛОГ
    // │   ├── 78901_123456\           # Уникальный каталог сборки (${CI_PIPELINE_ID}_${CI_JOB_ID})
    // │   │   ├── workspace\          # Рабочая область EDT
    // │   │   ├── temp_db\            # Временная БД 1С
    // │   │   ├── unload_xml\         # Выгруженные XML
	// │   │   ├── artifacts\		   # Артефакты (если настроен artifact upload)
    // │   │   └── logs\               # Логи
    // │   ├── cache\                  # Кэш между сборками
    // │   └── <старые_сборки>\        # Автоочистка runner
    // └── <остальное>
    КаталогСборки = ПолучитьПеременнуюСреды("CI_BUILDS_DIR");
    Если КаталогСборки = Неопределено Тогда
        КаталогСборки = ОбъединитьПути(СтартовыйСценарий().Каталог, "builds");
    КонецЕсли;

    PipelineID = ПолучитьПеременнуюСреды("CI_PIPELINE_ID");
    JobID = ПолучитьПеременнуюСреды("CI_JOB_ID");
    ВременныйКаталог = ОбъединитьПути(КаталогСборки, СтрШаблон("%1_%2", PipelineID, JobID));
    КаталогПроекта = ПолучитьПеременнуюСреды("CI_PROJECT_DIR");  
    КаталогРелизов = ОбъединитьПути(КаталогПроекта, "artifacts");

    // ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ ИСПОЛНИТЕЛЕЙ
    ВерсияПлатформы = ПолучитьПеременнуюСреды("ONEC_VERSION");
    ПутьКEDT = ПолучитьПеременнуюСреды("ONEC_EDTCLI_PATH");

    // СВЕДЕНИЯ PIPELINE
    ИмяПроекта = ПолучитьПеременнуюСреды("CI_PROJECT_NAME");
    ИмяВетки   = ПолучитьПеременнуюСреды("CI_COMMIT_REF_NAME");
    ХэшКоммита = ПолучитьПеременнуюСреды("CI_COMMIT_SHORT_SHA");
    
    // ПАРАМЕТРЫ УВЕДОМЛЕНИЙ TELEGRAM API
    TelegramBotID     = ПолучитьПеременнуюСреды("TG_BOT_ID");
    TelegramChannelID = ПолучитьПеременнуюСреды("TG_CHL_ID");

    Лог.Информация("=== Информация о сборке ===");
    Лог.Информация(СтрШаблон("Проект: %1", ?(ИмяПроекта <> Неопределено, ИмяПроекта, "Имя не установлено")));
    Лог.Информация(СтрШаблон("Ветка: %1", ?(ИмяВетки <> Неопределено, ИмяВетки, "Имя ветки не установлено")));
    Лог.Информация(СтрШаблон("Коммит: %1", ?(ХэшКоммита <> Неопределено, ХэшКоммита, "Коммит не установлен")));
    Лог.Информация(СтрШаблон("Pipeline: %1", ?(PipelineID <> Неопределено, PipelineID, "ID pipeline не установлен")));
    Лог.Информация(СтрШаблон("Версия платформы: %1", ВерсияПлатформы));

КонецПроцедуры

Процедура ВыполнитьСборкуКонфигурацийEDT()
    
    Попытка
        
        ИнициализироватьНачалоВыполнения();
        ПроверитьУстановитьПеременныеСреды();  
        УстановитьСведенияОКонфигурацииИРасширениях();
        ВыполнитьВыгрузкуПроектаВФайлы();
        ВыполнитьСборкуИзИсходников();
        ЗавершитьВыполнение();

    Исключение
        ОписаниеСборки = СтрШаблон("%1: Ветка %2", ИмяПроекта, ИмяВетки);
        ТекстОшибки = СтрШаблон("Сборка %1. Не удалось выполнить этап сборки: %2", ОписаниеСборки, ОписаниеОшибки());
        Лог.КритичнаяОшибка(ТекстОшибки);
        ОтправитьСообщениеВTelegram(ТекстОшибки);
        ЗавершитьВыполнение(1);

    КонецПопытки;

КонецПроцедуры

Процедура ЗавершитьВыполнение(КодВозврата = 0)

    ВремяЗавершения = Формат(ТекущаяДата(), "ДТ");
    Если КодВозврата = 0 Тогда
        ИнформацияОЗвершении = "Этап сборки завершен успешно"; 
    Иначе
        ИнформацияОЗвершении = "Этап сборки завершен с ошибками";
    КонецЕсли;

    Лог.Информация("%1: %2", ВремяЗавершения, ИнформацияОЗвершении);

    Лог.Закрыть();
    КопироватьФайл(ВрИмяФайлаЛога, ОбъединитьПути(КаталогРелизов, "oscript.build.log"));
    
    Если PipelineID = "TEST_PIPELINE_ID" Тогда
        УдалитьФайлы(ВременныйКаталог);
    КонецЕсли;

    ЗавершитьРаботу(КодВозврата);

КонецПроцедуры

Процедура ИнициализироватьНачалоВыполнения()
 
    ВременныйКаталог = ОбъединитьПути(КаталогВременныхФайлов(), "onec_build");
    СоздатьКаталог(ВременныйКаталог);

    КаталогЛогов = ОбъединитьПути(ВременныйКаталог, "log");
    СоздатьКаталог(КаталогЛогов);

    ВрИмяФайлаЛога = ОбъединитьПути(КаталогЛогов, "oscript.build.log");
    
    Лог = Логирование.ПолучитьЛог("oscript.build.messages");
    ФайлЖурнала = Новый ВыводЛогаВФайл;
    ФайлЖурнала.ОткрытьФайл(ВрИмяФайлаЛога);
    Лог.ДобавитьСпособВывода(ФайлЖурнала);
    Лог.ДобавитьСпособВывода(Новый ВыводЛогаВКонсоль);

КонецПроцедуры

Процедура УстановитьСведенияОКонфигурацииИРасширениях()

    ПутьВнутриПроекта = ОбъединитьПути(КаталогПроекта, "TRADE11");

    СведенияОКонфигурации = Новый Структура;
    СведенияОКонфигурации.Вставить("ИмяФормированияПути", "main");
    СведенияОКонфигурации.Вставить("ПутьВнутриПроекта", ПутьВнутриПроекта);
    
    ПутьВнутриПроекта = ОбъединитьПути(КаталогПроекта, "TRADE11.bit_ДоработкиАдаптация");
    ОписаниеРасширения = Новый Структура();
    ОписаниеРасширения.Вставить("Имя", "bit_ДоработкиАдаптация");
    ОписаниеРасширения.Вставить("ИмяФормированияПути", "bit_ДоработкиАдаптация");
    ОписаниеРасширения.Вставить("ПутьВнутриПроекта", ПутьВнутриПроекта);

    СведенияОРасширениях = Новый Массив;
    СведенияОРасширениях.Добавить(ОписаниеРасширения);
    
КонецПроцедуры

Процедура ОтправитьСообщениеВTelegram(ТекстСообщения)
    
    Попытка
        Если TelegramBotID <> Неопределено И TelegramChannelID <> Неопределено Тогда
            Мессенджер = Новый Мессенджер();
            Мессенджер.ИнициализироватьТранспорт("telegram", Новый Структура("Логин", TelegramBotID));
            ДополнительныеПараметры = Новый Структура;
            ДополнительныеПараметры.Вставить("КоличествоПопыток", 2);
            Мессенджер.ОтправитьСообщение("telegram", TelegramChannelID, ТекстСообщения, ДополнительныеПараметры);
        КонецЕсли;    
    Исключение
        Лог.Ошибка(СтрШаблон("Не удалось отправить сообщение в Telegram: %1", ОписаниеОшибки()));
    КонецПопытки;
    
КонецПроцедуры

ВыполнитьСборкуКонфигурацийEDT();
