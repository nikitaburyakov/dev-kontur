#Использовать logos
#Использовать v8runner
#Использовать irac

Перем Лог;                      // Лог    - Логирование

Перем ИмяПроекта;				// Строка - Имя проекта GitLab
Перем КаталогРелизов;           // Строка - Каталог релизов

Перем ВерсияПлатформы;          // Строка    - Версия платформы проекта
Перем СведенияОКонфигурации;    // Структура - Сведения о конфигурации
Перем СведенияОРасширениях;     // Структура - Сведения о расширениях


Перем ЭтоФайловаяБД;			// Булево - признак файловой БД
Перем Сервер1С;					// Строка - имя сервера 1С
Перем ПортСервера1С;			// Строка - порт менеджера кластера 1С (по умолчанию 1541)
Перем ИмяБД;					// Строка - Имя базы данных
Перем КаталогИБ;				// Строка - Каталог для файлово ИБ (ТипСервераБД = File)

Перем ИмяПользователяБД;		// Строка - Имя пользователя ИБ
Перем ПарольПользователяБД;		// Строка - Пароль пользователя ИБ

Перем СерверRAS;					 // Строка - Имя сервера администрирования
Перем ПортСервераRAS;				 // Строка - порт сервера администрирования
Перем АдминистраторRAS;				 // Строка - Имя администратора сервера RAS
Перем ПарольАдминистратораRAS;		 // Строка - Пароль администратора сервера RAS
Перем УправлениеКластером;			 // УправлениеКластером1С - менеджер управления кластером (irac)
Перем База1С;						 // ИнформационнаяБаза - Менеджер управления ИБ (irac)
Перем КодРазрешения;				 // Строка - Код разрешения сеанса
Перем ИсходныеПараметрыБлокировкиИБ; // Структура - Прочитанные настройки блокировки ИБ
Перем БлокировкиУстановлены;		 // Булево - контроль установки блокировок ИБ

Перем TelegramBotID;            // Строка    - ИД бота Telegram для уведомлений
Перем TelegramChannelID;        // Строка    - ИД канала Telegram для уведомлений

Перем РазделительЛога;			// Строка - Разделитель при выводе лога

Процедура ВыполнитьСкрипт()

	 Попытка
        
        ИнициализироватьНачалоВыполнения();
		УстановитьСведенияОКонфигурацииИРасширениях();  
        ВыполнитьЗагрузкуВИБ();
        ЗавершитьВыполнение();

    Исключение

		Если БлокировкиУстановлены = Истина Тогда
			СнятьБлокировкиИБ();
		КонецЕсли;

        ОписаниеСборки = СтрШаблон("%1: Загрузка файлов в ИБ", ИмяПроекта);
        ТекстОшибки = СтрШаблон("Сборка %1. Не удалось выполнить этап сборки: %2", ОписаниеСборки, ОписаниеОшибки());
        Лог.КритичнаяОшибка(ТекстОшибки);
        ОтправитьСообщениеВTelegram(ТекстОшибки);
        ЗавершитьВыполнение(1);

    КонецПопытки;	
	
КонецПроцедуры

// Проверяет установленные переменные среды (для целей отладки)
// Если переменная не установлена, выполняем установкой по текущей локальной конфигурации
Процедура ПроверитьУстановитьПеременныеСреды()

	Лог.Информация(РазделительЛога);
    Лог.Информация("УСТАНОВКА ПЕРЕМЕННЫХ СРЕДЫ");
	Лог.Информация(РазделительЛога);
    
    Переменные = Новый Соответствие;
        
    // Задаем переменные для тестирования и переопределения
    Переменные.Вставить("ONEC_VERSION",     			   "8.3.27.1859"); 
    Переменные.Вставить("DEPLOY_STAGE_V8_SERVER_NAME", 	   "localhost");
	Переменные.Вставить("DEPLOY_STAGE_V8_SERVER_PORT", 	   "1541");
	Переменные.Вставить("DEPLOY_STAGE_V8_SERVERRAS_NAME",  "localhost");
	Переменные.Вставить("DEPLOY_STAGE_V8_SERVERRAS_PORT",  "1545");
	Переменные.Вставить("DEPLOY_STAGE_V8_SERVERRAS_ADMIN", "");
	Переменные.Вставить("DEPLOY_STAGE_V8_SERVERRAS_PASS",  "");	
	Переменные.Вставить("DEPLOY_STAGE_DB_FILEPATH", 	   "E:\Buryakov\DB\testedt");
	Переменные.Вставить("DEPLOY_STAGE_DB_NAME", 		   "testdev");
	Переменные.Вставить("DEPLOY_STAGE_DB_USER", 		   "");
	Переменные.Вставить("DEPLOY_STAGE_DB_PASS", 		   "");
	Переменные.Вставить("DEPLOY_STAGE_DB_PERMISSION_CODE", "13792580");
	
    Для Каждого КлючИЗначение Из Переменные Цикл
        ЗначениеПеременной = ПолучитьПеременнуюСреды(КлючИЗначение.Ключ);
        Если ЗначениеПеременной = Неопределено Тогда
            Лог.Информация(СтрШаблон("Переменная среды %1 не была установлена", КлючИЗначение.Ключ));
            УстановитьПеременнуюСреды(КлючИЗначение.Ключ, КлючИЗначение.Значение);
        КонецЕсли;
    КонецЦикла;

	// ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ GITLAB
    КаталогРелизов = "artifacts";
	ИмяПроекта 	   = ПолучитьПеременнуюСреды("CI_PROJECT_NAME");

	// ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ ДОСТУПА К ИБ
	// Параметры доступа серверной ИБ
	Сервер1С 	   = ПолучитьПеременнуюСреды("DEPLOY_STAGE_V8_SERVER_NAME");
	ПортСервера1С  = ПолучитьПеременнуюСреды("DEPLOY_STAGE_V8_SERVER_PORT");
	ИмяБД 		   = ПолучитьПеременнуюСреды("DEPLOY_STAGE_DB_NAME");
	СерверRAS	   = ПолучитьПеременнуюСреды("DEPLOY_STAGE_V8_SERVERRAS_NAME");	
	ПортСервераRAS = ПолучитьПеременнуюСреды("DEPLOY_STAGE_V8_SERVERRAS_PORT");	
	АдминистраторRAS 		= ПолучитьПеременнуюСреды("DEPLOY_STAGE_V8_SERVERRAS_ADMIN");
	ПарольАдминистратораRAS	= ПолучитьПеременнуюСреды("DEPLOY_STAGE_V8_SERVERRAS_PASS");

	// Параметры доступа файловой ИБ
	КаталогИБ = ПолучитьПеременнуюСреды("DEPLOY_STAGE_DB_FILEPATH"); 

	// Параметры аутентификации ИБ
	ИмяПользователяБД	 = ПолучитьПеременнуюСреды("DEPLOY_STAGE_DB_USER");
	ПарольПользователяБД = ПолучитьПеременнуюСреды("DEPLOY_STAGE_DB_PASS");
	КодРазрешения 		 = ПолучитьПеременнуюСреды("DEPLOY_STAGE_DB_PERMISSION_CODE");

    // ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ ИСПОЛНИТЕЛЕЙ
    ВерсияПлатформы = ПолучитьПеременнуюСреды("ONEC_VERSION");

КонецПроцедуры

Процедура УстановитьСведенияОКонфигурацииИРасширениях()

    СведенияОКонфигурации = Новый Структура;
    СведенияОКонфигурации.Вставить("ИмяФайла", "1cv8.cf");
    
    ОписаниеРасширения = Новый Структура();
	ОписаниеРасширения.Вставить("Имя", "bit_ДоработкиАдаптация");
    ОписаниеРасширения.Вставить("ИмяФайла", "bit_ДоработкиАдаптация.cfe");

    СведенияОРасширениях = Новый Массив;
    СведенияОРасширениях.Добавить(ОписаниеРасширения);
    
КонецПроцедуры

Процедура ВыполнитьЗагрузкуВИБ()

	УстановитьБлокировкиИБ();
	
	ВыполнитьЗаершениеСеансовИБ();
	
	ЗагрузитьФайлыКонфигурацийВИБ();

	СнятьБлокировкиИБ();
	
КонецПроцедуры

Процедура УстановитьБлокировкиИБ()

	Если ЭтоФайловаяБД Тогда
		Возврат;
	КонецЕсли;

	Лог.Информация(РазделительЛога);
	Лог.Информация("Начало установки блокировки ИБ: " + ВремяВыполнения());

	ПараметрыБлокировки = ПараметрыБлокировкиИБ("УстановкаБлокировки");
	База1С.Изменить(ПараметрыБлокировки);

	БлокировкиУстановлены = Истина;

	Лог.Информация("Блокировки ИБ установлены: " + ВремяВыполнения());

КонецПроцедуры

Процедура ВыполнитьЗаершениеСеансовИБ()

	Если ЭтоФайловаяБД Тогда
		Возврат;
	КонецЕсли;

	Лог.Информация(РазделительЛога);
	Лог.Информация("Начало завершения сеансов ИБ: " + ВремяВыполнения());

	Для КоличествоПопыток = 1 По 10 Цикл
		СеансыЗавершены = Ложь;
		ЗавершитьСеансыИБ(База1С, СеансыЗавершены);
		Если СеансыЗавершены Тогда
			Прервать;
		КонецЕсли;
		КоличествоПопыток = КоличествоПопыток + 1;
	КонецЦикла;
	Если НЕ СеансыЗавершены Тогда 
		СнятьБлокировкиИБ();
		ВызватьИсключение("Не удалось завершить сеансы ИБ");
	КонецЕсли;

	Лог.Информация("Окончание завершения сеансов ИБ: " + ВремяВыполнения());

КонецПроцедуры

Процедура ЗавершитьСеансыИБ(База1С, СеансыЗавершены)

	База1С.ОбновитьДанные(1);
	СеансыБазы1С = База1С.Сеансы().Список();
	Для Каждого Сеанс Из СеансыБазы1С Цикл
		Попытка
			Сеанс.Завершить();
		Исключение
			Лог.Ошибка("Не удалось завершить сеанс ИБ");
		КонецПопытки;	
	КонецЦикла;

	База1С.ОбновитьДанные(1);
	СеансыБазы1С = База1С.Сеансы().Список();

	СеансыЗавершены = Истина;
	Если СеансыБазы1С.Количество() > 0 Тогда
		Для Каждого Сеанс Из СеансыБазы1С Цикл 
			ТипСоединения = Сеанс.Получить("app-id");
			Если ТипСоединения <> "SrvrConsole" Тогда 
				СеансыЗавершены = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ЗагрузитьФайлыКонфигурацийВИБ()

	Лог.Информация(РазделительЛога);

	УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
	ПараметрыСтрокиСоединения = УправлениеКонфигуратором.ПараметрыСтрокиСоединения();
	Если ЭтоФайловаяБД Тогда
		ПараметрыСтрокиСоединения.Вставить("ПутьКФайлуБазы", КаталогИБ);
	Иначе
		ПараметрыСтрокиСоединения.Вставить("Сервер", Сервер1С);
		ПараметрыСтрокиСоединения.Вставить("Порт", ПортСервера1С);
		ПараметрыСтрокиСоединения.Вставить("ИмяБазы", ИмяБД);
	КонецЕсли;

	СтрокаСоединения = УправлениеКонфигуратором.СформироватьСтрокуСоединения(ПараметрыСтрокиСоединения);
	УправлениеКонфигуратором.УстановитьКонтекст(СтрокаСоединения, ИмяПользователяБД, ПарольПользователяБД);
	Если НЕ ЭтоФайловаяБД Тогда
		УправлениеКонфигуратором.УстановитьКлючРазрешенияЗапуска(КодРазрешения);
	КонецЕсли;
	ПутьКФайлуКонфигурации = ОбъединитьПути(КаталогРелизов, СведенияОКонфигурации.ИмяФайла);
	Лог.Информация("Начало загрузки основной конфигурации (режим обновления)");
	УправлениеКонфигуратором.ЗагрузитьКонфигурациюИзФайла(ПутьКФайлуКонфигурации, Истина);
	Лог.Информация("Загрузка основной конфигурации завершена");

	Для Каждого ОписаниеРасширения Из СведенияОРасширениях Цикл
		ИмяРасширения = ОписаниеРасширения.Имя;

		ПутьКФайлуКонфигурации = ОбъединитьПути(КаталогРелизов, ОписаниеРасширения.ИмяФайла);
		Лог.Информация("Начало загрузки конфигурации расширения " + ИмяРасширения + " (режим обновления)");
		УправлениеКонфигуратором.ЗагрузитьРасширениеИзФайла(ПутьКФайлуКонфигурации, ИмяРасширения, Истина);
		Лог.Информация("Загрузка расширения " + ИмяРасширения + " завершена" );

	КонецЦикла;

КонецПроцедуры

Процедура СнятьБлокировкиИБ()

	Если ЭтоФайловаяБД Тогда
		Возврат;
	КонецЕсли;
	
	Лог.Информация("Начало снятия блокировки ИБ: " + ВремяВыполнения());

	ПараметрыБлокировки = ПараметрыБлокировкиИБ("СнятиеБлокировки");
	База1С.Изменить(ПараметрыБлокировки);

	БлокировкиУстановлены = Ложь;

	Лог.Информация("Блокировки ИБ сняты: " + ВремяВыполнения());

КонецПроцедуры

Функция ПараметрыБлокировкиИБ(Режим)

	ПараметрыБлокировки = Новый Структура();
	
	Если Режим = "УстановкаБлокировки" Тогда
		
		ТекстСообщенияБлокировки = "Информационная база заблокирована для технических работ";
		ПараметрыБлокировки.Вставить("БлокировкаСеансовВключена", "on");
		ПараметрыБлокировки.Вставить("БлокировкаРегламентныхЗаданийВключена", "on");
		ПараметрыБлокировки.Вставить("КодРазрешения", КодРазрешения);
		ПараметрыБлокировки.Вставить("СообщениеБлокировкиСеансов", ТекстСообщенияБлокировки);

	ИначеЕсли Режим = "СнятиеБлокировки" Тогда

		ПараметрыБлокировки.Вставить("БлокировкаСеансовВключена", "off");
		ПараметрыБлокировки.Вставить("БлокировкаРегламентныхЗаданийВключена", 
			ИсходныеПараметрыБлокировкиИБ.БлокировкаРегламентныхЗаданийВключена);

	Иначе

		ВызватьИсключение("Для изменения режима блокировок ИБ не передан режим");			

	КонецЕсли;

	Возврат ПараметрыБлокировки;

КонецФункции

Процедура ОтправитьСообщениеВTelegram(ТекстСообщения)

	КоличествоПопыток = 2;
	
    Попытка
        Если TelegramBotID <> Неопределено И TelegramChannelID <> Неопределено Тогда
            Мессенджер = Новый Мессенджер();
            Мессенджер.ИнициализироватьТранспорт("telegram", Новый Структура("Логин", TelegramBotID));
			ДополнительныеПараметры = Новый Структура;
            ДополнительныеПараметры.Вставить("КоличествоПопыток", КоличествоПопыток);
            Мессенджер.ОтправитьСообщение("telegram", TelegramChannelID, ТекстСообщения, ДополнительныеПараметры);
        КонецЕсли;    
    Исключение
        Лог.Ошибка(СтрШаблон("Не удалось отправить сообщение в Telegram: %1", ОписаниеОшибки()));
    КонецПопытки;
	
КонецПроцедуры

Процедура ИнициализироватьНачалоВыполнения()

	// ИНИЦИАЛИЗАЦИЯ ЛОГИРОВАНИЯ
    Лог = Логирование.ПолучитьЛог("oscript.deploy.messages");
    Лог.ДобавитьСпособВывода(Новый ВыводЛогаВКонсоль);

	// ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ СРЕДЫ
	ПроверитьУстановитьПеременныеСреды();

	ЭтоФайловаяБД = ЗначениеЗаполнено(КаталогИБ) И НЕ ЗначениеЗаполнено(ИмяБД);
	РазделительЛога = "================================================";
	
	Лог.Информация(РазделительЛога);
	Лог.Информация("НАЧАЛО ВЫПОЛНЕНИЯ СКРИПТА: " + ВремяВыполнения());
	Лог.Информация(РазделительЛога);
	Лог.Информация("");

	// ИНИЦИАЛИЗАЦИЯ УПРАВЛЕНИЯ ИБ
	Если НЕ ЭтоФайловаяБД Тогда
		Лог.Информация("Инициализация управления кластером 1С: " + ВремяВыполнения());
		СтрокаПодключенияКRAS = СтрШаблон("%1:%2", СерверRAS, ПортСервераRAS);
		АдминистраторКластера = Новый Структура;
		АдминистраторКластера.Вставить("Администратор", АдминистраторRAS);
		АдминистраторКластера.Вставить("Пароль", ПарольАдминистратораRAS);
		УправлениеКластером = Новый УправлениеКластером1С(ВерсияПлатформы, СтрокаПодключенияКRAS, АдминистраторКластера);
		Кластер = УправлениеКластером.Кластеры().Список()[0];
		БазыКластера = Кластер.ИнформационныеБазы();
		База1С = БазыКластера.Получить(ИмяБД, 1);
		База1С.УстановитьАдминистратора(ИмяПользователяБД, ПарольПользователяБД);
		
		ИсходныеПараметрыБлокировкиИБ = Новый Структура();
		ИсходныеПараметрыБлокировкиИБ.Вставить("БлокировкаРегламентныхЗаданийВключена", 
			База1С.Получить("БлокировкаРегламентныхЗаданийВключена", 1));
	КонецЕсли;

КонецПроцедуры

Процедура ЗавершитьВыполнение(КодВозврата = 0)

	Лог.Информация(РазделительЛога);
    Лог.Информация("ВЫПОЛНЕНИЕ ЗАВЕРШЕНО");
	Лог.Информация(РазделительЛога);

    ВремяЗавершения = Формат(ТекущаяДата(), "ДТ");
    Если КодВозврата = 0 Тогда
        ИнформацияОЗавершении = "Этап загрузки файлов в ИБ завершен успешно"; 
    Иначе
        ИнформацияОЗавершении = "Этап загрузки файлов в ИБ завершен с ошибками";
    КонецЕсли;

	Лог.Информация(РазделительЛога);
    Лог.Информация("%1: %2", ВремяЗавершения, ИнформацияОЗавершении);
    Лог.Закрыть();

    ЗавершитьРаботу(КодВозврата);

КонецПроцедуры

Функция ВремяВыполнения()
	Возврат Формат(ТекущаяДата(), "ДТ");
КонецФункции

ВыполнитьСкрипт();